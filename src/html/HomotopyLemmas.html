<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>HomotopyLemmas</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Markup">::

</a>   <a id="8" class="Symbol">{-#</a> <a id="12" class="Keyword">OPTIONS</a> <a id="20" class="Pragma">--without-K</a> <a id="32" class="Pragma">--exact-split</a> <a id="46" class="Symbol">#-}</a>
   <a id="53" class="Keyword">open</a> <a id="58" class="Keyword">import</a> <a id="65" href="Transport.html" class="Module">Transport</a>
   <a id="78" class="Keyword">open</a> <a id="83" class="Keyword">import</a> <a id="90" href="HomotopyType.html" class="Module">HomotopyType</a>

<a id="104" class="Background">Composition with homotopies
~~~~~~~~~~~~~~~~~~~~~~~~~~~

</a><a id="161" class="Markup">::

</a>   <a id="hl-comp"></a><a id="168" href="HomotopyLemmas.html#168" class="Function">hl-comp</a>
     <a id="181" class="Symbol">:</a> <a id="183" class="Symbol">∀</a> <a id="185" class="Symbol">{</a><a id="186" href="HomotopyLemmas.html#186" class="Bound">ℓ₁</a> <a id="189" href="HomotopyLemmas.html#189" class="Bound">ℓ₂</a> <a id="192" href="HomotopyLemmas.html#192" class="Bound">ℓ₃</a> <a id="195" class="Symbol">:</a> <a id="197" href="Agda.Primitive.html#423" class="Postulate">Level</a><a id="202" class="Symbol">}</a> <a id="204" class="Symbol">{</a><a id="205" href="HomotopyLemmas.html#205" class="Bound">A</a> <a id="207" class="Symbol">:</a> <a id="209" href="Intro.html#1611" class="Function">Type</a> <a id="214" href="HomotopyLemmas.html#186" class="Bound">ℓ₁</a><a id="216" class="Symbol">}{</a><a id="218" href="HomotopyLemmas.html#218" class="Bound">B</a> <a id="220" class="Symbol">:</a> <a id="222" href="Intro.html#1611" class="Function">Type</a> <a id="227" href="HomotopyLemmas.html#189" class="Bound">ℓ₂</a><a id="229" class="Symbol">}</a> <a id="231" class="Symbol">{</a><a id="232" href="HomotopyLemmas.html#232" class="Bound">C</a> <a id="234" class="Symbol">:</a> <a id="236" href="Intro.html#1611" class="Function">Type</a> <a id="241" href="HomotopyLemmas.html#192" class="Bound">ℓ₃</a><a id="243" class="Symbol">}</a>
     <a id="250" class="Symbol">→</a> <a id="252" class="Symbol">{</a><a id="253" href="HomotopyLemmas.html#253" class="Bound">f</a> <a id="255" href="HomotopyLemmas.html#255" class="Bound">g</a> <a id="257" class="Symbol">:</a> <a id="259" href="HomotopyLemmas.html#205" class="Bound">A</a> <a id="261" class="Symbol">→</a> <a id="263" href="HomotopyLemmas.html#218" class="Bound">B</a><a id="264" class="Symbol">}</a>
     <a id="271" class="Symbol">→</a> <a id="273" class="Symbol">{</a><a id="274" href="HomotopyLemmas.html#274" class="Bound">j</a> <a id="276" href="HomotopyLemmas.html#276" class="Bound">k</a> <a id="278" class="Symbol">:</a> <a id="280" href="HomotopyLemmas.html#218" class="Bound">B</a> <a id="282" class="Symbol">→</a> <a id="284" href="HomotopyLemmas.html#232" class="Bound">C</a><a id="285" class="Symbol">}</a>
     <a id="292" class="Symbol">→</a> <a id="294" href="HomotopyLemmas.html#253" class="Bound">f</a> <a id="296" href="HomotopyType.html#769" class="Function Operator">∼</a> <a id="298" href="HomotopyLemmas.html#255" class="Bound">g</a>
     <a id="305" class="Symbol">→</a> <a id="307" href="HomotopyLemmas.html#274" class="Bound">j</a> <a id="309" href="HomotopyType.html#769" class="Function Operator">∼</a> <a id="311" href="HomotopyLemmas.html#276" class="Bound">k</a>
     <a id="318" class="Comment">-------------------</a>
     <a id="343" class="Symbol">→</a> <a id="345" class="Symbol">(</a><a id="346" href="HomotopyLemmas.html#274" class="Bound">j</a> <a id="348" href="BasicFunctions.html#1675" class="Function Operator">∘</a> <a id="350" href="HomotopyLemmas.html#253" class="Bound">f</a><a id="351" class="Symbol">)</a> <a id="353" href="HomotopyType.html#769" class="Function Operator">∼</a> <a id="355" class="Symbol">(</a><a id="356" href="HomotopyLemmas.html#276" class="Bound">k</a> <a id="358" href="BasicFunctions.html#1675" class="Function Operator">∘</a> <a id="360" href="HomotopyLemmas.html#255" class="Bound">g</a><a id="361" class="Symbol">)</a>

   <a id="367" href="HomotopyLemmas.html#168" class="Function">hl-comp</a> <a id="375" class="Symbol">{</a><a id="376" class="Argument">g</a> <a id="378" class="Symbol">=</a> <a id="380" href="HomotopyLemmas.html#380" class="Bound">g</a><a id="381" class="Symbol">}{</a><a id="383" class="Argument">j</a> <a id="385" class="Symbol">=</a> <a id="387" href="HomotopyLemmas.html#387" class="Bound">j</a><a id="388" class="Symbol">}</a> <a id="390" href="HomotopyLemmas.html#390" class="Bound">f-g</a> <a id="394" href="HomotopyLemmas.html#394" class="Bound">j-k</a> <a id="398" class="Symbol">=</a> <a id="400" class="Symbol">λ</a> <a id="402" href="HomotopyLemmas.html#402" class="Bound">x</a> <a id="404" class="Symbol">→</a> <a id="406" href="AlgebraOnPaths.html#175" class="Function">ap</a> <a id="409" href="HomotopyLemmas.html#387" class="Bound">j</a> <a id="411" class="Symbol">(</a><a id="412" href="HomotopyLemmas.html#390" class="Bound">f-g</a> <a id="416" href="HomotopyLemmas.html#402" class="Bound">x</a><a id="417" class="Symbol">)</a> <a id="419" href="BasicFunctions.html#198" class="Function Operator">·</a> <a id="421" href="HomotopyLemmas.html#394" class="Bound">j-k</a> <a id="425" class="Symbol">(</a><a id="426" href="HomotopyLemmas.html#380" class="Bound">g</a> <a id="428" href="HomotopyLemmas.html#402" class="Bound">x</a><a id="429" class="Symbol">)</a>

<a id="432" class="Markup">::

</a>   <a id="rcomp-∼"></a><a id="439" href="HomotopyLemmas.html#439" class="Function">rcomp-∼</a>
     <a id="452" class="Symbol">:</a> <a id="454" class="Symbol">∀</a> <a id="456" class="Symbol">{</a><a id="457" href="HomotopyLemmas.html#457" class="Bound">ℓ₁</a> <a id="460" href="HomotopyLemmas.html#460" class="Bound">ℓ₂</a> <a id="463" href="HomotopyLemmas.html#463" class="Bound">ℓ₃</a> <a id="466" class="Symbol">:</a> <a id="468" href="Agda.Primitive.html#423" class="Postulate">Level</a><a id="473" class="Symbol">}</a> <a id="475" class="Symbol">{</a><a id="476" href="HomotopyLemmas.html#476" class="Bound">A</a> <a id="478" class="Symbol">:</a> <a id="480" href="Intro.html#1611" class="Function">Type</a> <a id="485" href="HomotopyLemmas.html#457" class="Bound">ℓ₁</a><a id="487" class="Symbol">}{</a><a id="489" href="HomotopyLemmas.html#489" class="Bound">B</a> <a id="491" class="Symbol">:</a> <a id="493" href="Intro.html#1611" class="Function">Type</a> <a id="498" href="HomotopyLemmas.html#460" class="Bound">ℓ₂</a><a id="500" class="Symbol">}</a> <a id="502" class="Symbol">{</a><a id="503" href="HomotopyLemmas.html#503" class="Bound">C</a> <a id="505" class="Symbol">:</a> <a id="507" href="Intro.html#1611" class="Function">Type</a> <a id="512" href="HomotopyLemmas.html#463" class="Bound">ℓ₃</a><a id="514" class="Symbol">}</a>
     <a id="521" class="Symbol">→</a> <a id="523" class="Symbol">(</a><a id="524" href="HomotopyLemmas.html#524" class="Bound">f</a> <a id="526" class="Symbol">:</a> <a id="528" href="HomotopyLemmas.html#476" class="Bound">A</a> <a id="530" class="Symbol">→</a> <a id="532" href="HomotopyLemmas.html#489" class="Bound">B</a><a id="533" class="Symbol">)</a>
     <a id="540" class="Symbol">→</a> <a id="542" class="Symbol">{</a><a id="543" href="HomotopyLemmas.html#543" class="Bound">j</a> <a id="545" href="HomotopyLemmas.html#545" class="Bound">k</a> <a id="547" class="Symbol">:</a> <a id="549" href="HomotopyLemmas.html#489" class="Bound">B</a> <a id="551" class="Symbol">→</a> <a id="553" href="HomotopyLemmas.html#503" class="Bound">C</a><a id="554" class="Symbol">}</a>
     <a id="561" class="Symbol">→</a> <a id="563" href="HomotopyLemmas.html#543" class="Bound">j</a> <a id="565" href="HomotopyType.html#769" class="Function Operator">∼</a> <a id="567" href="HomotopyLemmas.html#545" class="Bound">k</a>
     <a id="574" class="Comment">-------------------</a>
     <a id="599" class="Symbol">→</a> <a id="601" class="Symbol">(</a><a id="602" href="HomotopyLemmas.html#543" class="Bound">j</a> <a id="604" href="BasicFunctions.html#1675" class="Function Operator">∘</a> <a id="606" href="HomotopyLemmas.html#524" class="Bound">f</a><a id="607" class="Symbol">)</a> <a id="609" href="HomotopyType.html#769" class="Function Operator">∼</a> <a id="611" class="Symbol">(</a><a id="612" href="HomotopyLemmas.html#545" class="Bound">k</a> <a id="614" href="BasicFunctions.html#1675" class="Function Operator">∘</a> <a id="616" href="HomotopyLemmas.html#524" class="Bound">f</a><a id="617" class="Symbol">)</a>

   <a id="623" href="HomotopyLemmas.html#439" class="Function">rcomp-∼</a> <a id="631" href="HomotopyLemmas.html#631" class="Bound">f</a> <a id="633" href="HomotopyLemmas.html#633" class="Bound">j-k</a> <a id="637" class="Symbol">=</a> <a id="639" href="HomotopyLemmas.html#168" class="Function">hl-comp</a> <a id="647" class="Symbol">(</a><a id="648" href="HomotopyType.html#993" class="Function">h-refl</a> <a id="655" href="HomotopyLemmas.html#631" class="Bound">f</a><a id="656" class="Symbol">)</a> <a id="658" href="HomotopyLemmas.html#633" class="Bound">j-k</a>

<a id="663" class="Markup">::

</a>   <a id="lcomp-∼"></a><a id="670" href="HomotopyLemmas.html#670" class="Function">lcomp-∼</a>
     <a id="683" class="Symbol">:</a> <a id="685" class="Symbol">∀</a> <a id="687" class="Symbol">{</a><a id="688" href="HomotopyLemmas.html#688" class="Bound">ℓ₁</a> <a id="691" href="HomotopyLemmas.html#691" class="Bound">ℓ₂</a> <a id="694" href="HomotopyLemmas.html#694" class="Bound">ℓ₃</a> <a id="697" class="Symbol">:</a> <a id="699" href="Agda.Primitive.html#423" class="Postulate">Level</a><a id="704" class="Symbol">}</a> <a id="706" class="Symbol">{</a><a id="707" href="HomotopyLemmas.html#707" class="Bound">A</a> <a id="709" class="Symbol">:</a> <a id="711" href="Intro.html#1611" class="Function">Type</a> <a id="716" href="HomotopyLemmas.html#688" class="Bound">ℓ₁</a><a id="718" class="Symbol">}{</a><a id="720" href="HomotopyLemmas.html#720" class="Bound">B</a> <a id="722" class="Symbol">:</a> <a id="724" href="Intro.html#1611" class="Function">Type</a> <a id="729" href="HomotopyLemmas.html#691" class="Bound">ℓ₂</a><a id="731" class="Symbol">}</a> <a id="733" class="Symbol">{</a><a id="734" href="HomotopyLemmas.html#734" class="Bound">C</a> <a id="736" class="Symbol">:</a> <a id="738" href="Intro.html#1611" class="Function">Type</a> <a id="743" href="HomotopyLemmas.html#694" class="Bound">ℓ₃</a><a id="745" class="Symbol">}</a>
     <a id="752" class="Symbol">→</a> <a id="754" class="Symbol">{</a><a id="755" href="HomotopyLemmas.html#755" class="Bound">f</a> <a id="757" href="HomotopyLemmas.html#757" class="Bound">g</a> <a id="759" class="Symbol">:</a> <a id="761" href="HomotopyLemmas.html#707" class="Bound">A</a> <a id="763" class="Symbol">→</a> <a id="765" href="HomotopyLemmas.html#720" class="Bound">B</a><a id="766" class="Symbol">}</a>
     <a id="773" class="Symbol">→</a> <a id="775" class="Symbol">(</a><a id="776" href="HomotopyLemmas.html#776" class="Bound">j</a> <a id="778" class="Symbol">:</a> <a id="780" href="HomotopyLemmas.html#720" class="Bound">B</a> <a id="782" class="Symbol">→</a> <a id="784" href="HomotopyLemmas.html#734" class="Bound">C</a><a id="785" class="Symbol">)</a>
     <a id="792" class="Symbol">→</a> <a id="794" href="HomotopyLemmas.html#755" class="Bound">f</a> <a id="796" href="HomotopyType.html#769" class="Function Operator">∼</a> <a id="798" href="HomotopyLemmas.html#757" class="Bound">g</a>
     <a id="805" class="Comment">-------------------</a>
     <a id="830" class="Symbol">→</a> <a id="832" class="Symbol">(</a><a id="833" href="HomotopyLemmas.html#776" class="Bound">j</a> <a id="835" href="BasicFunctions.html#1675" class="Function Operator">∘</a> <a id="837" href="HomotopyLemmas.html#755" class="Bound">f</a><a id="838" class="Symbol">)</a> <a id="840" href="HomotopyType.html#769" class="Function Operator">∼</a> <a id="842" class="Symbol">(</a><a id="843" href="HomotopyLemmas.html#776" class="Bound">j</a> <a id="845" href="BasicFunctions.html#1675" class="Function Operator">∘</a> <a id="847" href="HomotopyLemmas.html#757" class="Bound">g</a><a id="848" class="Symbol">)</a>

   <a id="854" href="HomotopyLemmas.html#670" class="Function">lcomp-∼</a> <a id="862" href="HomotopyLemmas.html#862" class="Bound">j</a> <a id="864" href="HomotopyLemmas.html#864" class="Bound">α</a> <a id="866" class="Symbol">=</a> <a id="868" href="HomotopyLemmas.html#168" class="Function">hl-comp</a> <a id="876" href="HomotopyLemmas.html#864" class="Bound">α</a> <a id="878" class="Symbol">(</a><a id="879" href="HomotopyType.html#993" class="Function">h-refl</a> <a id="886" href="HomotopyLemmas.html#862" class="Bound">j</a><a id="887" class="Symbol">)</a>

<a id="890" class="Background">Naturality
~~~~~~~~~~

Homotopy is natural, meaning that it satisfies the following square
commutative diagram.

</a><a id="1003" class="Markup">::

</a>   <a id="h-naturality"></a><a id="1010" href="HomotopyLemmas.html#1010" class="Function">h-naturality</a>
     <a id="1028" class="Symbol">:</a> <a id="1030" class="Symbol">∀</a> <a id="1032" class="Symbol">{</a><a id="1033" href="HomotopyLemmas.html#1033" class="Bound">ℓ₁</a> <a id="1036" href="HomotopyLemmas.html#1036" class="Bound">ℓ₂</a> <a id="1039" class="Symbol">:</a> <a id="1041" href="Agda.Primitive.html#423" class="Postulate">Level</a><a id="1046" class="Symbol">}</a> <a id="1048" class="Symbol">{</a><a id="1049" href="HomotopyLemmas.html#1049" class="Bound">A</a> <a id="1051" class="Symbol">:</a> <a id="1053" href="Intro.html#1611" class="Function">Type</a> <a id="1058" href="HomotopyLemmas.html#1033" class="Bound">ℓ₁</a><a id="1060" class="Symbol">}{</a><a id="1062" href="HomotopyLemmas.html#1062" class="Bound">B</a> <a id="1064" class="Symbol">:</a> <a id="1066" href="Intro.html#1611" class="Function">Type</a> <a id="1071" href="HomotopyLemmas.html#1036" class="Bound">ℓ₂</a><a id="1073" class="Symbol">}</a>
     <a id="1080" class="Symbol">→</a> <a id="1082" class="Symbol">{</a><a id="1083" href="HomotopyLemmas.html#1083" class="Bound">f</a> <a id="1085" href="HomotopyLemmas.html#1085" class="Bound">g</a> <a id="1087" class="Symbol">:</a> <a id="1089" href="HomotopyLemmas.html#1049" class="Bound">A</a> <a id="1091" class="Symbol">→</a> <a id="1093" href="HomotopyLemmas.html#1062" class="Bound">B</a><a id="1094" class="Symbol">}</a> <a id="1096" class="Symbol">→</a> <a id="1098" class="Symbol">{</a><a id="1099" href="HomotopyLemmas.html#1099" class="Bound">x</a> <a id="1101" href="HomotopyLemmas.html#1101" class="Bound">y</a> <a id="1103" class="Symbol">:</a> <a id="1105" href="HomotopyLemmas.html#1049" class="Bound">A</a><a id="1106" class="Symbol">}</a>
     <a id="1113" class="Symbol">→</a> <a id="1115" class="Symbol">(</a><a id="1116" href="HomotopyLemmas.html#1116" class="Bound">H</a> <a id="1118" class="Symbol">:</a> <a id="1120" href="HomotopyLemmas.html#1083" class="Bound">f</a> <a id="1122" href="HomotopyType.html#769" class="Function Operator">∼</a> <a id="1124" href="HomotopyLemmas.html#1085" class="Bound">g</a><a id="1125" class="Symbol">)</a>
     <a id="1132" class="Symbol">→</a> <a id="1134" class="Symbol">(</a><a id="1135" href="HomotopyLemmas.html#1135" class="Bound">p</a> <a id="1137" class="Symbol">:</a> <a id="1139" href="HomotopyLemmas.html#1099" class="Bound">x</a> <a id="1141" href="BasicTypes.html#9886" class="Datatype Operator">==</a> <a id="1144" href="HomotopyLemmas.html#1101" class="Bound">y</a><a id="1145" class="Symbol">)</a>
     <a id="1152" class="Comment">------------------------------</a>
     <a id="1188" class="Symbol">→</a> <a id="1190" href="HomotopyLemmas.html#1116" class="Bound">H</a> <a id="1192" href="HomotopyLemmas.html#1099" class="Bound">x</a> <a id="1194" href="BasicFunctions.html#198" class="Function Operator">·</a> <a id="1196" href="AlgebraOnPaths.html#175" class="Function">ap</a> <a id="1199" href="HomotopyLemmas.html#1085" class="Bound">g</a> <a id="1201" href="HomotopyLemmas.html#1135" class="Bound">p</a> <a id="1203" href="BasicTypes.html#9886" class="Datatype Operator">==</a> <a id="1206" href="AlgebraOnPaths.html#175" class="Function">ap</a> <a id="1209" href="HomotopyLemmas.html#1083" class="Bound">f</a> <a id="1211" href="HomotopyLemmas.html#1135" class="Bound">p</a> <a id="1213" href="BasicFunctions.html#198" class="Function Operator">·</a> <a id="1215" href="HomotopyLemmas.html#1116" class="Bound">H</a> <a id="1217" href="HomotopyLemmas.html#1101" class="Bound">y</a>

   <a id="1223" href="HomotopyLemmas.html#1010" class="Function">h-naturality</a> <a id="1236" class="Symbol">{</a><a id="1237" class="Argument">x</a> <a id="1239" class="Symbol">=</a> <a id="1241" href="HomotopyLemmas.html#1241" class="Bound">x</a><a id="1242" class="Symbol">}</a> <a id="1244" href="HomotopyLemmas.html#1244" class="Bound">H</a> <a id="1246" href="BasicTypes.html#9959" class="InductiveConstructor">idp</a> <a id="1250" class="Symbol">=</a> <a id="1252" href="BasicFunctions.html#562" class="Function Operator">!</a> <a id="1254" class="Symbol">(</a><a id="1255" href="AlgebraOnPaths.html#2389" class="Function">·-runit</a> <a id="1263" class="Symbol">(</a><a id="1264" href="HomotopyLemmas.html#1244" class="Bound">H</a> <a id="1266" href="HomotopyLemmas.html#1241" class="Bound">x</a><a id="1267" class="Symbol">))</a>

<a id="1271" class="Background">A particular case of naturality on the identity function.

</a><a id="1330" class="Markup">::

</a>   <a id="h-naturality-id"></a><a id="1337" href="HomotopyLemmas.html#1337" class="Function">h-naturality-id</a>
     <a id="1358" class="Symbol">:</a> <a id="1360" class="Symbol">∀</a> <a id="1362" class="Symbol">{</a><a id="1363" href="HomotopyLemmas.html#1363" class="Bound">ℓ</a> <a id="1365" class="Symbol">:</a> <a id="1367" href="Agda.Primitive.html#423" class="Postulate">Level</a><a id="1372" class="Symbol">}</a> <a id="1374" class="Symbol">{</a><a id="1375" href="HomotopyLemmas.html#1375" class="Bound">A</a> <a id="1377" class="Symbol">:</a> <a id="1379" href="Intro.html#1611" class="Function">Type</a> <a id="1384" href="HomotopyLemmas.html#1363" class="Bound">ℓ</a><a id="1385" class="Symbol">}</a>  <a id="1388" class="Symbol">{</a><a id="1389" href="HomotopyLemmas.html#1389" class="Bound">f</a> <a id="1391" class="Symbol">:</a> <a id="1393" href="HomotopyLemmas.html#1375" class="Bound">A</a> <a id="1395" class="Symbol">→</a> <a id="1397" href="HomotopyLemmas.html#1375" class="Bound">A</a><a id="1398" class="Symbol">}</a> <a id="1400" class="Symbol">→</a> <a id="1402" class="Symbol">{</a><a id="1403" href="HomotopyLemmas.html#1403" class="Bound">x</a> <a id="1405" class="Symbol">:</a> <a id="1407" href="HomotopyLemmas.html#1375" class="Bound">A</a><a id="1408" class="Symbol">}</a>
     <a id="1415" class="Symbol">→</a> <a id="1417" class="Symbol">(</a><a id="1418" href="HomotopyLemmas.html#1418" class="Bound">H</a> <a id="1420" class="Symbol">:</a> <a id="1422" href="HomotopyLemmas.html#1389" class="Bound">f</a> <a id="1424" href="HomotopyType.html#769" class="Function Operator">∼</a> <a id="1426" href="BasicFunctions.html#683" class="Function">id</a><a id="1428" class="Symbol">)</a>
     <a id="1435" class="Comment">-----------------------</a>
     <a id="1464" class="Symbol">→</a> <a id="1466" href="HomotopyLemmas.html#1418" class="Bound">H</a> <a id="1468" class="Symbol">(</a><a id="1469" href="HomotopyLemmas.html#1389" class="Bound">f</a> <a id="1471" href="HomotopyLemmas.html#1403" class="Bound">x</a><a id="1472" class="Symbol">)</a> <a id="1474" href="BasicTypes.html#9886" class="Datatype Operator">==</a> <a id="1477" href="AlgebraOnPaths.html#175" class="Function">ap</a> <a id="1480" href="HomotopyLemmas.html#1389" class="Bound">f</a> <a id="1482" class="Symbol">(</a><a id="1483" href="HomotopyLemmas.html#1418" class="Bound">H</a> <a id="1485" href="HomotopyLemmas.html#1403" class="Bound">x</a><a id="1486" class="Symbol">)</a>

   <a id="1492" href="HomotopyLemmas.html#1337" class="Function">h-naturality-id</a> <a id="1508" class="Symbol">{</a><a id="1509" class="Argument">f</a> <a id="1511" class="Symbol">=</a> <a id="1513" href="HomotopyLemmas.html#1513" class="Bound">f</a><a id="1514" class="Symbol">}</a> <a id="1516" class="Symbol">{</a><a id="1517" class="Argument">x</a> <a id="1519" class="Symbol">=</a> <a id="1521" href="HomotopyLemmas.html#1521" class="Bound">x</a><a id="1522" class="Symbol">}</a> <a id="1524" href="HomotopyLemmas.html#1524" class="Bound">H</a> <a id="1526" class="Symbol">=</a>
     <a id="1533" href="BasicFunctions.html#10619" class="Function Operator">begin</a>
       <a id="1546" href="HomotopyLemmas.html#1524" class="Bound">H</a> <a id="1548" class="Symbol">(</a><a id="1549" href="HomotopyLemmas.html#1513" class="Bound">f</a> <a id="1551" href="HomotopyLemmas.html#1521" class="Bound">x</a><a id="1552" class="Symbol">)</a>
         <a id="1563" href="BasicFunctions.html#10294" class="Function Operator">==⟨</a> <a id="1567" href="AlgebraOnPaths.html#2389" class="Function">·-runit</a> <a id="1575" class="Symbol">(</a><a id="1576" href="HomotopyLemmas.html#1524" class="Bound">H</a> <a id="1578" class="Symbol">(</a><a id="1579" href="HomotopyLemmas.html#1513" class="Bound">f</a> <a id="1581" href="HomotopyLemmas.html#1521" class="Bound">x</a><a id="1582" class="Symbol">))</a> <a id="1585" href="BasicFunctions.html#10294" class="Function Operator">⟩</a>
       <a id="1594" href="HomotopyLemmas.html#1524" class="Bound">H</a> <a id="1596" class="Symbol">(</a><a id="1597" href="HomotopyLemmas.html#1513" class="Bound">f</a> <a id="1599" href="HomotopyLemmas.html#1521" class="Bound">x</a><a id="1600" class="Symbol">)</a> <a id="1602" href="BasicFunctions.html#198" class="Function Operator">·</a> <a id="1604" href="BasicTypes.html#10472" class="Function">refl</a> <a id="1609" class="Symbol">(</a><a id="1610" href="HomotopyLemmas.html#1513" class="Bound">f</a> <a id="1612" href="HomotopyLemmas.html#1521" class="Bound">x</a><a id="1613" class="Symbol">)</a>
         <a id="1624" href="BasicFunctions.html#10294" class="Function Operator">==⟨</a> <a id="1628" href="AlgebraOnPaths.html#175" class="Function">ap</a> <a id="1631" class="Symbol">(</a><a id="1632" href="HomotopyLemmas.html#1524" class="Bound">H</a> <a id="1634" class="Symbol">(</a><a id="1635" href="HomotopyLemmas.html#1513" class="Bound">f</a> <a id="1637" href="HomotopyLemmas.html#1521" class="Bound">x</a><a id="1638" class="Symbol">)</a> <a id="1640" href="BasicFunctions.html#198" class="Function Operator">·_</a><a id="1642" class="Symbol">)</a> <a id="1644" class="Symbol">(</a><a id="1645" href="BasicFunctions.html#562" class="Function Operator">!</a> <a id="1647" class="Symbol">(</a><a id="1648" href="AlgebraOnPaths.html#3104" class="Function">·-rinv</a> <a id="1655" class="Symbol">(</a><a id="1656" href="HomotopyLemmas.html#1524" class="Bound">H</a> <a id="1658" href="HomotopyLemmas.html#1521" class="Bound">x</a><a id="1659" class="Symbol">)))</a> <a id="1663" href="BasicFunctions.html#10294" class="Function Operator">⟩</a>
       <a id="1672" href="HomotopyLemmas.html#1524" class="Bound">H</a> <a id="1674" class="Symbol">(</a><a id="1675" href="HomotopyLemmas.html#1513" class="Bound">f</a> <a id="1677" href="HomotopyLemmas.html#1521" class="Bound">x</a><a id="1678" class="Symbol">)</a> <a id="1680" href="BasicFunctions.html#198" class="Function Operator">·</a> <a id="1682" class="Symbol">((</a><a id="1684" href="HomotopyLemmas.html#1524" class="Bound">H</a> <a id="1686" href="HomotopyLemmas.html#1521" class="Bound">x</a><a id="1687" class="Symbol">)</a> <a id="1689" href="BasicFunctions.html#198" class="Function Operator">·</a> <a id="1691" class="Symbol">(</a><a id="1692" href="BasicFunctions.html#562" class="Function Operator">!</a> <a id="1694" class="Symbol">(</a><a id="1695" href="HomotopyLemmas.html#1524" class="Bound">H</a> <a id="1697" href="HomotopyLemmas.html#1521" class="Bound">x</a><a id="1698" class="Symbol">)))</a>
         <a id="1711" href="BasicFunctions.html#10294" class="Function Operator">==⟨</a> <a id="1715" href="AlgebraOnPaths.html#175" class="Function">ap</a> <a id="1718" class="Symbol">(</a><a id="1719" href="HomotopyLemmas.html#1524" class="Bound">H</a> <a id="1721" class="Symbol">(</a><a id="1722" href="HomotopyLemmas.html#1513" class="Bound">f</a> <a id="1724" href="HomotopyLemmas.html#1521" class="Bound">x</a><a id="1725" class="Symbol">)</a> <a id="1727" href="BasicFunctions.html#198" class="Function Operator">·_</a><a id="1729" class="Symbol">)</a> <a id="1731" class="Symbol">(</a><a id="1732" href="AlgebraOnPaths.html#175" class="Function">ap</a> <a id="1735" class="Symbol">(</a><a id="1736" href="BasicFunctions.html#198" class="Function Operator">_·</a> <a id="1739" class="Symbol">(</a><a id="1740" href="BasicFunctions.html#562" class="Function Operator">!</a> <a id="1742" class="Symbol">(</a><a id="1743" href="HomotopyLemmas.html#1524" class="Bound">H</a> <a id="1745" href="HomotopyLemmas.html#1521" class="Bound">x</a><a id="1746" class="Symbol">)))</a> <a id="1750" class="Symbol">(</a><a id="1751" href="BasicFunctions.html#562" class="Function Operator">!</a> <a id="1753" href="AlgebraOnPaths.html#1930" class="Function">ap-id</a> <a id="1759" class="Symbol">(</a><a id="1760" href="HomotopyLemmas.html#1524" class="Bound">H</a> <a id="1762" href="HomotopyLemmas.html#1521" class="Bound">x</a><a id="1763" class="Symbol">)))</a> <a id="1767" href="BasicFunctions.html#10294" class="Function Operator">⟩</a>
       <a id="1776" href="HomotopyLemmas.html#1524" class="Bound">H</a> <a id="1778" class="Symbol">(</a><a id="1779" href="HomotopyLemmas.html#1513" class="Bound">f</a> <a id="1781" href="HomotopyLemmas.html#1521" class="Bound">x</a><a id="1782" class="Symbol">)</a> <a id="1784" href="BasicFunctions.html#198" class="Function Operator">·</a> <a id="1786" class="Symbol">(</a><a id="1787" href="AlgebraOnPaths.html#175" class="Function">ap</a> <a id="1790" href="BasicFunctions.html#683" class="Function">id</a> <a id="1793" class="Symbol">(</a><a id="1794" href="HomotopyLemmas.html#1524" class="Bound">H</a> <a id="1796" href="HomotopyLemmas.html#1521" class="Bound">x</a><a id="1797" class="Symbol">)</a> <a id="1799" href="BasicFunctions.html#198" class="Function Operator">·</a> <a id="1801" href="BasicFunctions.html#562" class="Function Operator">!</a> <a id="1803" class="Symbol">(</a><a id="1804" href="HomotopyLemmas.html#1524" class="Bound">H</a> <a id="1806" href="HomotopyLemmas.html#1521" class="Bound">x</a><a id="1807" class="Symbol">))</a>
         <a id="1819" href="BasicFunctions.html#10294" class="Function Operator">==⟨</a> <a id="1823" href="BasicFunctions.html#562" class="Function Operator">!</a> <a id="1825" class="Symbol">(</a><a id="1826" href="AlgebraOnPaths.html#3433" class="Function">·-assoc</a> <a id="1834" class="Symbol">(</a><a id="1835" href="HomotopyLemmas.html#1524" class="Bound">H</a> <a id="1837" class="Symbol">(</a><a id="1838" href="HomotopyLemmas.html#1513" class="Bound">f</a> <a id="1840" href="HomotopyLemmas.html#1521" class="Bound">x</a><a id="1841" class="Symbol">))</a> <a id="1844" class="Symbol">(</a><a id="1845" href="AlgebraOnPaths.html#175" class="Function">ap</a> <a id="1848" href="BasicFunctions.html#683" class="Function">id</a> <a id="1851" class="Symbol">(</a><a id="1852" href="HomotopyLemmas.html#1524" class="Bound">H</a> <a id="1854" href="HomotopyLemmas.html#1521" class="Bound">x</a><a id="1855" class="Symbol">))</a> <a id="1858" class="Symbol">(</a><a id="1859" href="BasicFunctions.html#562" class="Function Operator">!</a> <a id="1861" class="Symbol">(</a><a id="1862" href="HomotopyLemmas.html#1524" class="Bound">H</a> <a id="1864" href="HomotopyLemmas.html#1521" class="Bound">x</a><a id="1865" class="Symbol">)))</a> <a id="1869" href="BasicFunctions.html#10294" class="Function Operator">⟩</a>
       <a id="1878" class="Symbol">(</a><a id="1879" href="HomotopyLemmas.html#1524" class="Bound">H</a> <a id="1881" class="Symbol">(</a><a id="1882" href="HomotopyLemmas.html#1513" class="Bound">f</a> <a id="1884" href="HomotopyLemmas.html#1521" class="Bound">x</a><a id="1885" class="Symbol">)</a> <a id="1887" href="BasicFunctions.html#198" class="Function Operator">·</a> <a id="1889" href="AlgebraOnPaths.html#175" class="Function">ap</a> <a id="1892" href="BasicFunctions.html#683" class="Function">id</a> <a id="1895" class="Symbol">(</a><a id="1896" href="HomotopyLemmas.html#1524" class="Bound">H</a> <a id="1898" href="HomotopyLemmas.html#1521" class="Bound">x</a><a id="1899" class="Symbol">))</a> <a id="1902" href="BasicFunctions.html#198" class="Function Operator">·</a> <a id="1904" href="BasicFunctions.html#562" class="Function Operator">!</a> <a id="1906" class="Symbol">(</a><a id="1907" href="HomotopyLemmas.html#1524" class="Bound">H</a> <a id="1909" href="HomotopyLemmas.html#1521" class="Bound">x</a><a id="1910" class="Symbol">)</a>
         <a id="1921" href="BasicFunctions.html#10294" class="Function Operator">==⟨</a> <a id="1925" href="AlgebraOnPaths.html#5149" class="Function">·-right-to-left-r</a> <a id="1943" class="Symbol">(</a><a id="1944" href="HomotopyLemmas.html#1010" class="Function">h-naturality</a> <a id="1957" href="HomotopyLemmas.html#1524" class="Bound">H</a> <a id="1959" class="Symbol">(</a><a id="1960" href="HomotopyLemmas.html#1524" class="Bound">H</a> <a id="1962" href="HomotopyLemmas.html#1521" class="Bound">x</a><a id="1963" class="Symbol">))</a> <a id="1966" href="BasicFunctions.html#10294" class="Function Operator">⟩</a>
       <a id="1975" href="AlgebraOnPaths.html#175" class="Function">ap</a> <a id="1978" href="HomotopyLemmas.html#1513" class="Bound">f</a> <a id="1980" class="Symbol">(</a><a id="1981" href="HomotopyLemmas.html#1524" class="Bound">H</a> <a id="1983" href="HomotopyLemmas.html#1521" class="Bound">x</a><a id="1984" class="Symbol">)</a>
     <a id="1991" href="BasicFunctions.html#10509" class="Function Operator">∎</a>
</pre></body></html>